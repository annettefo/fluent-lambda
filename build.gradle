/*******************************************************************************
 * Copyright 2014 Carlos Macasaet
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

group = 'com.macasaet.lambda'
ext.archivesBaseName = 'fluent-lambda'
version = '0.0.3-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith( 'SNAPSHOT' )
project.ext.sourceCompatibility = 1.5

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
}

jar {
  manifest {
    attributes 'Specification-Title': 'Fluent Lambda',
      'Specification-Version': '0.0',
      'Implementation-Title': group,
      'Implementation-Version': version
  }
}

task writeNewPom << {
  pom {
    project {
      inceptionYear '2014'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }.writeTo( "${buildDir/newpom.xml}" )
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar( type: Jar ) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask( 'uploadArchives' ) }
  sign configurations.archives
}

uploadArchives {
  // required { isReleaseVersion }
  onlyIf { isReleaseVersion }
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom( deployment ) }

      repository( url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/" ) {
        authentication( userName: ossrhUsername )
      }

      snapshotRepository( url: "https://oss.sonatype.org/content/repositories/snapshots/" ) {
        authentication( userName: ossrhUsername )
      }

      pom.project {
        name 'Fluent Lambda'
        packaging 'jar'
        artifactId archivesBaseName
        description ''
        url 'https://github.com/l0s/fluent-lambda'

        scm {
          connection 'scm:git@github.com:l0s/fluent-lambda.git'
          developerConnection 'scm:git@github.com:l0s/fluent-lambda.git'
          url 'https://github.com/l0s/fluent-lambda'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'carlos'
            name 'Carlos Macasaet'
          }
        }
      }
    }
  }
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '10.0'
  compile group: 'cglib', name: 'cglib-nodep', version: '2.2.2'
  compile group: 'commons-lang', name: 'commons-lang', version: '2.5'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.apache.commons', name: 'commons-math', version: '2.2'
  testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.1'
}
