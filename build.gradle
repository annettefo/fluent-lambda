/*******************************************************************************
 * Copyright 2014 Carlos Macasaet
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

group = 'com.macasaet.lambda'
ext.archivesBaseName = 'fluent-lambda'
version = '0.2.2-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith( 'SNAPSHOT' )
project.ext.sourceCompatibility = 1.5

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

repositories {
  mavenCentral()
}

compileJava {
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

jar {
  manifest {
    attributes 'Specification-Title': 'Fluent Lambda',
      'Specification-Version': '0.0',
      'Implementation-Title': group,
      'Implementation-Version': version
  }
}

task writeNewPom {
  doLast {
    pom {
      project {
        inceptionYear '2014'
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
      }
    }.writeTo( "${buildDir/newpom.xml}" )
  }
}

modifyPom {
  project {
    name 'Fluent Lambda'
    packaging 'jar'
    artifactId archivesBaseName
    description 'Fluent interfaces for creating lambda expressions in Java'
    url 'https://github.com/l0s/fluent-lambda'

    scm {
      connection 'scm:git@github.com:l0s/fluent-lambda.git'
      developerConnection 'scm:git@github.com:l0s/fluent-lambda.git'
      url 'https://github.com/l0s/fluent-lambda'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      }
    }

    developers {
      developer {
        id 'l0s'
        name 'Carlos Macasaet'
      }
    }
  }
}

if( project.hasProperty( "signing.gnupg.keyName" ) ) {
  signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask( "uploadArchives" ) }
    useGpgCmd()
  }
}


dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '10.0'
  compile group: 'cglib', name: 'cglib', version: '2.2.2'
  compile group: 'commons-lang', name: 'commons-lang', version: '2.5'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.apache.commons', name: 'commons-math', version: '2.2'
  testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.1'
}
